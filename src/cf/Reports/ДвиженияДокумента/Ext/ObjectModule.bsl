///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма, Неопределено - Форма отчета или форма настроек отчета.
//       Неопределено когда вызов без контекста.
//   КлючВарианта - Строка, Неопределено - Имя предопределенного
//       или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов без контекста.
//   Настройки - Структура - см. возвращаемое значение
//       ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.РазрешеноВыбиратьИНастраиватьВариантыБезСохранения = Истина;
	Настройки.СкрытьКомандыРассылки                              = Истина;
	Настройки.ПараметрыРасположенияЭлементовУправления           = ПараметрыРасположенияЭлементовУправления();
	
	Настройки.События.ПриСозданииНаСервере               = Истина;
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	Настройки.События.ПередЗагрузкойВариантаНаСервере    = Истина;
	Настройки.События.ПриОпределенииПараметровВыбора     = Истина;
	
	Настройки.ФормироватьСразу = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//   Например, если схема отчета зависит от ключа варианта или параметров отчета.
//   Чтобы изменения схемы вступили в силу следует вызывать метод ОтчетыСервер.ПодключитьСхему().
//
// Параметры:
//   Контекст - Произвольный - 
//       Параметры контекста, в котором используется отчет.
//       Используется для передачи в параметрах метода ОтчетыСервер.ПодключитьСхему().
//   КлючСхемы - Строка -
//       Идентификатор текущей схемы компоновщика настроек.
//       По умолчанию не заполнен (это означает что компоновщик инициализирован на основании основной схемы).
//       Используется для оптимизации, чтобы переинициализировать компоновщик как можно реже.
//       Может не использоваться если переинициализация выполняется безусловно.
//   КлючВарианта - Строка, Неопределено -
//       Имя предопределенного или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов для варианта расшифровки или без контекста.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных, Неопределено -
//       Настройки варианта отчета, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено когда настройки варианта не надо загружать (уже загружены ранее).
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных, Неопределено -
//       Пользовательские настройки, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено когда пользовательские настройки не надо загружать (уже загружены ранее).
//
// Варианты вызова:
//   Если КлючСхемы <> "1" Тогда
//   	КлючСхемы = "1";
//   	СхемаКД = ПолучитьОбщийМакет("МояОбщаяСхемаКомпоновки");
//   	ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКД, КлючСхемы);
//   КонецЕсли; - компоновщик отчета инициализируется на основании схемы из общих макетов.
//   Если ТипЗнч(НовыеПользовательскиеНастройкиКД) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
//   	ПолноеИмяОбъектаМетаданных = "";
//   	Для Каждого ЭлементКД Из НовыеПользовательскиеНастройкиКД.Элементы Цикл
//   		Если ТипЗнч(ЭлементКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
//   			ИмяПараметра = Строка(ЭлементКД.Параметр);
//   			Если ИмяПараметра = "ОбъектМетаданных" Тогда
//   				ПолноеИмяОбъектаМетаданных = ЭлементКД.Значение;
//   			КонецЕсли;
//   		КонецЕсли;
//   	КонецЦикла;
//   	Если КлючСхемы <> ПолноеИмяОбъектаМетаданных Тогда
//   		КлючСхемы = ПолноеИмяОбъектаМетаданных;
//   		СхемаКД = Новый СхемаКомпоновкиДанных;
//   		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКД, КлючСхемы);
//   	КонецЕсли;
//   КонецЕсли; - схема зависит от значения параметра, выведенного в пользовательские настройки отчета.
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД,
	НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если ТипЗнч(НовыеНастройкиКД) = Тип("НастройкиКомпоновкиДанных") Тогда
		НастройкиКД = НовыеНастройкиКД;
	Иначе
		НастройкиКД = КомпоновщикНастроек.Настройки;
	КонецЕсли;
	
	ПараметрыОтчета = ПараметрыОтчета(НастройкиКД, НовыеПользовательскиеНастройкиКД);
	
	Если ПараметрыОтчета.ДокументВладелец = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Отчет о движениях документа не предназначен для рассылки.'");
	КонецЕсли;
	
	Если КлючСхемы <> КлючВарианта Тогда
		
		КлючСхемы = КлючВарианта;
		
		Если КлючВарианта = "Основной" Тогда
			ПодготовитьГоризонтальныйВариант(ПараметрыОтчета.ДокументВладелец,
				СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
		ИначеЕсли КлючВарианта = "Дополнительный" Тогда
			ПодготовитьВертикальныйВариант(ПараметрыОтчета.ДокументВладелец,
				СхемаКомпоновкиДанных.НастройкиПоУмолчанию, Истина);
		КонецЕсли;
		
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
		
		НастройкиКДПоУмолчаниюТиповойСхемы(СхемаКомпоновкиДанных, ПараметрыОтчета, НовыеНастройкиКД, КлючВарианта);
		
	КонецЕсли;
	
КонецПроцедуры

// См. ОтчетыПереопределяемый.ПриСозданииНаСервере.
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Форма.Параметры.ПредставлениеВарианта = "Расшифровка" Тогда
		ВызватьИсключение НСтр("ru = 'Выбранное действие в данном отчете не доступно.'");
	КонецЕсли;
	
	Если Форма.Параметры.Свойство("ДокументВладелец") Тогда
		СтруктураПараметровДанных = Новый Структура("ДокументВладелец", Форма.Параметры.ДокументВладелец);
		УстановитьПараметрыДанных(КомпоновщикНастроек.Настройки, СтруктураПараметровДанных);
	ИначеЕсли Не Форма.Параметры.Свойство("ПараметрКоманды") Тогда
		ВызватьИсключение НСтр("ru = 'Для начала работы с отчетом необходимо воспользоваться соответствующей командой в форме интересующего документа.'");
	КонецЕсли;
	ДокументВладелец = Форма.Параметры.ПараметрКоманды;
	Если Не ЗначениеЗаполнено(ДокументВладелец) Тогда
		ВызватьИсключение НСтр("ru = 'Для начала работы с отчетом необходимо воспользоваться соответствующей командой в форме интересующего документа.'");
	КонецЕсли;
	
	СтруктураПараметровДанных = Новый Структура("ДокументВладелец", ДокументВладелец);
	УстановитьПараметрыДанных(КомпоновщикНастроек.Настройки, СтруктураПараметровДанных);
	Форма.КлючНазначенияИспользования = ДокументВладелец.Метаданные().ПолноеИмя();
	
КонецПроцедуры

// См. ОтчетыПереопределяемый.ПриОпределенииПараметровВыбора.
Процедура ПриОпределенииПараметровВыбора(Форма, СвойстваНастройки) Экспорт
	
	ПолеИмяРегистра = Новый ПолеКомпоновкиДанных("ПараметрыДанных.СписокРегистров");
	Если СвойстваНастройки.ПолеКД = ПолеИмяРегистра Тогда
		
		Если КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Свойство("ГлобальныеНастройки") Тогда
			
			СвойстваНастройки.ЗначенияДляВыбора.Очистить();
			СвойстваНастройки.ОграничиватьВыборУказаннымиЗначениями = Истина;
			ГлобальныеНастройки = ГлобальныеНастройки();
			Для Каждого ГлобальнаяНастройка Из ГлобальныеНастройки Цикл
				ЗначениеДляВыбора = "Регистр" + ГлобальнаяНастройка.ВидРегистра + "_" + ГлобальнаяНастройка.ИмяРегистра;
				Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
					НРег(ГлобальнаяНастройка.ПредставлениеВидаРегистра), ГлобальнаяНастройка.ПредставлениеРегистра);
				СвойстваНастройки.ЗначенияДляВыбора.Добавить(ЗначениеДляВыбора, Представление);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// См. ОтчетыПереопределяемый.ПередЗагрузкойВариантаНаСервере.
Процедура ПередЗагрузкойВариантаНаСервере(Форма, НовыеНастройкиКД) Экспорт
	
	ПараметрСКД         = Новый ПараметрКомпоновкиДанных("ДокументВладелец");
	ДокументПараметрСКД = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ПараметрСКД);
	
	Если ДокументПараметрСКД <> Неопределено Тогда
		ДокументПараметр = ДокументПараметрСКД.Значение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументПараметр) Тогда
		
		УстановитьПараметрыДанных(НовыеНастройкиКД, Новый Структура("ДокументВладелец", ДокументПараметр));
		
		Если Форма.КлючТекущегоВарианта = Форма.Отчет.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.КлючВарианта Тогда
			
			СкопироватьНастройкиКомпоновкиДанных(НовыеНастройкиКД, КомпоновщикНастроек.Настройки);
			
			Если Форма.КлючТекущегоВарианта = "Основной" Тогда
				ПодготовитьГоризонтальныйВариант(ДокументПараметр, СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
			ИначеЕсли Форма.КлючТекущегоВарианта = "Дополнительный" Тогда
				ПодготовитьВертикальныйВариант(ДокументПараметр, СхемаКомпоновкиДанных.НастройкиПоУмолчанию, Истина);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДокументПараметрСКД = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ДокументВладелец");
	Если ДокументПараметрСКД = Неопределено Или Не ЗначениеЗаполнено(ДокументПараметрСКД.Значение) Тогда
		Возврат;
	Иначе
		ДокументОтбор = ДокументПараметрСКД.Значение;
	КонецЕсли;
	
	МакетЗаголовка                              = ПолучитьМакет("Заголовок");
	ОбластьЗаголовка                            = МакетЗаголовка.ПолучитьОбласть("ОбластьЗаголовок");
	ОбластьЗаголовка.Параметры.СсылкаНаДокумент = Строка(ДокументОтбор);
	ПустаяОбласть                               = МакетЗаголовка.ПолучитьОбласть("ПустаяОбласть");
	
	ОбластьЗаголовка.ТекущаяОбласть.Расшифровка = ДокументОтбор;
	
	ДокументРезультат.Вывести(ПустаяОбласть);
	ДокументРезультат.Вывести(ОбластьЗаголовка);
	ДокументРезультат.Вывести(ПустаяОбласть);
	
	НастройкиКД = КомпоновщикНастроек.ПолучитьНастройки();
	
	СкрытьНеактуальныеГруппировки(НастройкиКД, ДокументОтбор);
	ВосстановитьОтборПоГруппировкамРегистров(НастройкиКД);
	НастроитьУсловноеОформление(СхемаКомпоновкиДанных, НастройкиКД);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки   = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКД, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ОформитьРезультат(ДокументРезультат, ДокументОтбор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПрограммноеФормированиеСКД

#Область ПодготовкаДанных

Функция НаборыДанныхДляОтчета(ДокументОтбор, ДвиженияДокумента, ТаблицаРегистров, ДополнительнаяНумерация = Ложь)
	
	НаборыДанных = Новый Массив;
	
	Для Каждого СведенияОРегистре Из ТаблицаРегистров Цикл
		
		ВидРегистра = СведенияОРегистре.ВидРегистра;
		НаборДанных = Неопределено;
		Если ВидРегистра = "Накопления" Или ВидРегистра = "Сведений" Тогда
			НаборДанных = НаборДанныхДляРегистраНакопленияСведений(СведенияОРегистре, ДополнительнаяНумерация);
		ИначеЕсли ВидРегистра = "Бухгалтерии" Тогда
			НаборДанных = НаборДанныхДляРегистраБухгалтерии(СведенияОРегистре);
		ИначеЕсли ВидРегистра = "Расчета" Тогда
			НаборДанных = НаборДанныхДляРегистраРасчета(СведенияОРегистре, ДополнительнаяНумерация);
		КонецЕсли;
		Если НаборДанных <> Неопределено Тогда
			НаборыДанных.Добавить(НаборДанных);
		КонецЕсли;
		
	КонецЦикла;
	
	ОтчетОДвиженияхДокументаПереопределяемый.ПриПодготовкеНабораДанных(ДокументОтбор, НаборыДанных);
	Возврат НаборыДанных;
	
КонецФункции

Функция НаборДанныхДляРегистраНакопленияСведений(СведенияОРегистре, ДополнительнаяНумерация)
	
	НаборДанных = Новый Структура;
	НаборДанных.Вставить("Измерения",              Новый Структура);
	НаборДанных.Вставить("Ресурсы",                Новый Структура);
	НаборДанных.Вставить("Реквизиты",              Новый Структура);
	НаборДанных.Вставить("СтандартныеРеквизиты",   Новый Структура);
	НаборДанных.Вставить("ПолноеИмяРегистра",      СтрЗаменить(СведенияОРегистре.Регистр.ПолноеИмя(), ".", "_"));
	НаборДанных.Вставить("ИмяРегистра",            СведенияОРегистре.ИмяРегистра);
	НаборДанных.Вставить("ВидРегистра",            СведенияОРегистре.ВидРегистра);
	НаборДанных.Вставить("ПредставлениеВидаРегистра", СведенияОРегистре.ПредставлениеВидаРегистра);
	НаборДанных.Вставить("ПредставлениеРегистра",  СведенияОРегистре.ПредставлениеРегистра);
	
	Если ДополнительнаяНумерация Тогда
		НаборДанных.Вставить("Нумератор", Новый Структура);
	КонецЕсли;
	
	ДвижениеДокумента = СведенияОРегистре.Регистр;
	НаборДанных.СтандартныеРеквизиты = ИменаПредставленияПолей(ДвижениеДокумента.СтандартныеРеквизиты, "НомерСтроки, Регистратор");
	НаборДанных.Измерения            = ИменаПредставленияПолей(ДвижениеДокумента.Измерения);
	НаборДанных.Ресурсы              = ИменаПредставленияПолей(ДвижениеДокумента.Ресурсы);
	НаборДанных.Реквизиты            = ИменаПредставленияПолей(ДвижениеДокумента.Реквизиты);
	
	ПоляВыбора = "";
	ДобавитьПоля(ПоляВыбора, НаборДанных.СтандартныеРеквизиты, ДвижениеДокумента);
	ДобавитьПоля(ПоляВыбора, НаборДанных.Измерения, ДвижениеДокумента, "Измерения");
	ДобавитьПоля(ПоляВыбора, НаборДанных.Ресурсы, ДвижениеДокумента, "Ресурсы");
	ДобавитьПоля(ПоляВыбора, НаборДанных.Реквизиты, ДвижениеДокумента, "Реквизиты");
	
	Если ДополнительнаяНумерация Тогда
	
		НаборДанных.Нумератор = НомераПолей(НаборДанных);
		ДобавитьНомераПолей(ПоляВыбора, НаборДанных.Нумератор);
	
	КонецЕсли;
	
	Если СокрЛП(Прав(ПоляВыбора, 2)) = "," Тогда
		ПоляВыбора = Лев(ПоляВыбора, СтрДлина(ПоляВыбора) - 2);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК КоличествоЗаписейРегистра,
	|	""&ИмяРегистра"" КАК ИмяРегистра,
	|	&Поля
	|ИЗ
	|	&ТекущаяТаблица КАК ТекущаяТаблица
	|ГДЕ
	|	&УсловиеЗапроса
	|{ГДЕ
	|	(&УсловиеКомпоновки)}";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяРегистра", НаборДанных.ПолноеИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Поля", ПоляВыбора);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекущаяТаблица", ДвижениеДокумента.ПолноеИмя());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеЗапроса", СведенияОРегистре.ПолеРегистратора + " = &ДокументВладелец");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеКомпоновки", """" + НаборДанных.ПолноеИмяРегистра + """ В (&СписокРегистров)");
	НаборДанных.Вставить("ТекстЗапроса", ТекстЗапроса);
	
	Возврат НаборДанных;
	
КонецФункции

Функция НаборДанныхДляРегистраБухгалтерии(СведенияОРегистре)
	
	НаборДанных = Новый Структура;
	НаборДанных.Вставить("СтандартныеРеквизиты",   Новый Структура);
	НаборДанных.Вставить("Измерения",              Новый Структура);
	НаборДанных.Вставить("Ресурсы",                Новый Структура);
	НаборДанных.Вставить("РесурсыДт",              Новый Структура);
	НаборДанных.Вставить("РесурсыКт",              Новый Структура);
	НаборДанных.Вставить("Субконто",               Новый Структура);
	НаборДанных.Вставить("СубконтоДт",             Новый Структура);
	НаборДанных.Вставить("СубконтоКт",             Новый Структура);
	НаборДанных.Вставить("ИзмеренияДт",            Новый Структура);
	НаборДанных.Вставить("ИзмеренияКт",            Новый Структура);
	НаборДанных.Вставить("Реквизиты",              Новый Структура);
	НаборДанных.Вставить("ПолноеИмяРегистра",      СтрЗаменить(СведенияОРегистре.Регистр.ПолноеИмя(), ".", "_"));
	НаборДанных.Вставить("ИмяРегистра",            СведенияОРегистре.ИмяРегистра);
	НаборДанных.Вставить("ВидРегистра",            СведенияОРегистре.ВидРегистра);
	НаборДанных.Вставить("ПредставлениеВидаРегистра", СведенияОРегистре.ПредставлениеВидаРегистра);
	НаборДанных.Вставить("ПредставлениеРегистра",  СведенияОРегистре.ПредставлениеРегистра);
	
	ДвижениеДокумента = СведенияОРегистре.Регистр;
	МаксимальноеКоличествоСубконто = ДвижениеДокумента.ПланСчетов.МаксКоличествоСубконто;
	КорреспонденцияОбъекта         = ДвижениеДокумента.Корреспонденция;
	
	ЛокализацияДебет    = НСтр("ru = 'Дт'");
	ЛокализацияКредит   = НСтр("ru = 'Кт'");
	ЛокализацияСубконто = НСтр("ru = 'Субконто'");
	ЛокализацияСчет     = НСтр("ru = 'Счет'");
	
	СтандартныеРеквизитыИсключения = "НомерСтроки, Регистратор, Счет";
	Для ИндексСубконто = 1 По МаксимальноеКоличествоСубконто Цикл
		ИндексСубконтоСтрокой =  Формат(ИндексСубконто, "ЧГ=0");
		СтандартныеРеквизитыИсключения = СтандартныеРеквизитыИсключения 
			+ ", " + "ВидСубконто" + ИндексСубконтоСтрокой + ", Субконто" + ИндексСубконтоСтрокой;
	КонецЦикла;
	
	НаборДанных.СтандартныеРеквизиты = ИменаПредставленияПолей(ДвижениеДокумента.СтандартныеРеквизиты,
		СтандартныеРеквизитыИсключения);
	НаборДанных.Реквизиты            = ИменаПредставленияПолей(ДвижениеДокумента.Реквизиты,
		СтандартныеРеквизитыИсключения);
	
	Ресурсы = ДвижениеДокумента.Ресурсы;
	Для Каждого Ресурс Из Ресурсы Цикл
	
		Если Ресурс.Балансовый Или Не КорреспонденцияОбъекта Тогда
	
			НаборДанных.Ресурсы.Вставить(Ресурс.Имя, Ресурс.Представление());
	
		Иначе
	
			НаборДанных.РесурсыДт.Вставить(Ресурс.Имя + "Дт", Ресурс.Представление() + " " + ЛокализацияДебет);
			НаборДанных.РесурсыКт.Вставить(Ресурс.Имя + "Кт", Ресурс.Представление() + " " + ЛокализацияКредит);
	
		КонецЕсли;
	
	КонецЦикла;
	
	Для ИндексСубконто = 1 По МаксимальноеКоличествоСубконто Цикл
	
		Если КорреспонденцияОбъекта Тогда
	
			ИндексСтрокой = Формат(ИндексСубконто, "ЧГ=0");
			
			НаборДанных.СубконтоДт.Вставить("СубконтоДт" + ИндексСтрокой,
				ЛокализацияСубконто + " " + ЛокализацияДебет + " " + ИндексСтрокой);
			НаборДанных.СубконтоКт.Вставить("СубконтоКт" + ИндексСтрокой,
				ЛокализацияСубконто + " " + ЛокализацияКредит + " " + ИндексСтрокой);
	
		Иначе
	
			НаборДанных.Субконто.Вставить("Субконто" + ИндексСтрокой, ЛокализацияСубконто + " " + ИндексСтрокой);
	
		КонецЕсли;
		
	КонецЦикла;
	
	Если КорреспонденцияОбъекта Тогда
		
		НаборДанных.ИзмеренияДт.Вставить("СчетДт", ЛокализацияСчет + " " +ЛокализацияДебет);
		НаборДанных.ИзмеренияКт.Вставить("СчетКт", ЛокализацияСчет + " " + ЛокализацияКредит);
	
	Иначе
	
		ТекстИзмерения = ТекстИзмерения + ?(ЗначениеЗаполнено(ТекстИзмерения), ", ", "") + "Счет";
	
		НаборДанных.Измерения.Вставить("Счет", ЛокализацияСчет);
	
	КонецЕсли;
	
	Измерения = ДвижениеДокумента.Измерения;
	Для Каждого Измерение Из Измерения Цикл
	
		Если Измерение.Балансовый Или Не КорреспонденцияОбъекта Тогда
	
			НаборДанных.Измерения.Вставить(Измерение.Имя, Измерение.Представление());
	
		Иначе
	
			НаборДанных.ИзмеренияДт.Вставить(Измерение.Имя + "Дт", Измерение.Представление() + " " + ЛокализацияДебет);
			НаборДанных.ИзмеренияКт.Вставить(Измерение.Имя + "Кт", Измерение.Представление() + " " + ЛокализацияКредит);
	
		КонецЕсли;
	
	КонецЦикла;
	
	ПоляВыбора = "";
	ДобавитьПоля(ПоляВыбора, НаборДанных.СтандартныеРеквизиты, ДвижениеДокумента);
	ДобавитьПоля(ПоляВыбора, НаборДанных.Реквизиты, ДвижениеДокумента, "Реквизиты");
	ДобавитьПоля(ПоляВыбора, НаборДанных.Измерения, ДвижениеДокумента, "Измерения");
	ДобавитьПоля(ПоляВыбора, НаборДанных.ИзмеренияДт, ДвижениеДокумента);
	ДобавитьПоля(ПоляВыбора, НаборДанных.ИзмеренияКт, ДвижениеДокумента);
	ДобавитьПоля(ПоляВыбора, НаборДанных.Ресурсы, ДвижениеДокумента, "Ресурсы");
	ДобавитьПоля(ПоляВыбора, НаборДанных.РесурсыДт, ДвижениеДокумента);
	ДобавитьПоля(ПоляВыбора, НаборДанных.РесурсыКт, ДвижениеДокумента);
	ДобавитьПоля(ПоляВыбора, НаборДанных.Субконто, ДвижениеДокумента, "Субконто");
	ДобавитьПоля(ПоляВыбора, НаборДанных.СубконтоДт, ДвижениеДокумента);
	ДобавитьПоля(ПоляВыбора, НаборДанных.СубконтоКт, ДвижениеДокумента);
	
	Если СокрЛП(Прав(ПоляВыбора, 2)) = "," Тогда
		ПоляВыбора = Лев(ПоляВыбора, СтрДлина(ПоляВыбора) - 2);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК КоличествоЗаписейРегистра,
	|	""&ИмяРегистра"" КАК ИмяРегистра,
	|	&Поля
	|ИЗ
	|	&ТекущаяТаблица КАК ТекущаяТаблица
	|ГДЕ
	|	&УсловиеЗапроса
	|{ГДЕ
	|	(&УсловиеКомпоновки)}";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяРегистра", НаборДанных.ПолноеИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Поля", ПоляВыбора);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекущаяТаблица", "РегистрБухгалтерии." + СведенияОРегистре.ИмяРегистра
		+ ".ДвиженияССубконто(, , " + СведенияОРегистре.ПолеРегистратора + " = &ДокументВладелец)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеЗапроса", СведенияОРегистре.ПолеРегистратора + " = &ДокументВладелец");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеКомпоновки", """" + НаборДанных.ПолноеИмяРегистра + """ В (&СписокРегистров)");
	
	НаборДанных.Вставить("ТекстЗапроса", ТекстЗапроса);
	
	Возврат НаборДанных;
	
КонецФункции

Функция НаборДанныхДляРегистраРасчета(СведенияОРегистре, ДополнительнаяНумерация)
	
	НаборДанных = Новый Структура;
	НаборДанных.Вставить("Измерения",              Новый Структура);
	НаборДанных.Вставить("Ресурсы",                Новый Структура);
	НаборДанных.Вставить("Реквизиты",              Новый Структура);
	НаборДанных.Вставить("СтандартныеРеквизиты",   Новый Структура);
	НаборДанных.Вставить("ПолноеИмяРегистра",      СтрЗаменить(СведенияОРегистре.Регистр.ПолноеИмя(), ".", "_"));
	НаборДанных.Вставить("ИмяРегистра",            СведенияОРегистре.ИмяРегистра);
	НаборДанных.Вставить("ВидРегистра",            СведенияОРегистре.ВидРегистра);
	НаборДанных.Вставить("ПредставлениеВидаРегистра", СведенияОРегистре.ПредставлениеВидаРегистра);
	НаборДанных.Вставить("ПредставлениеРегистра",  СведенияОРегистре.ПредставлениеРегистра);
	
	Если ДополнительнаяНумерация Тогда
		НаборДанных.Вставить("Нумератор", Новый Структура);
	КонецЕсли;
	
	ДвижениеДокумента = СведенияОРегистре.Регистр;
	Для Каждого Реквизит Из ДвижениеДокумента.СтандартныеРеквизиты Цикл
		НаборДанных.СтандартныеРеквизиты.Вставить(Реквизит.Имя, Реквизит.Представление());
	КонецЦикла;
	
	НаборДанных.Измерения = ИменаПредставленияПолей(ДвижениеДокумента.Измерения);
	НаборДанных.Ресурсы   = ИменаПредставленияПолей(ДвижениеДокумента.Ресурсы);
	НаборДанных.Реквизиты = ИменаПредставленияПолей(ДвижениеДокумента.Реквизиты);
	
	ПоляВыбора = "";
	ДобавитьПоля(ПоляВыбора, НаборДанных.СтандартныеРеквизиты, ДвижениеДокумента);
	ДобавитьПоля(ПоляВыбора, НаборДанных.Измерения, ДвижениеДокумента, "Измерения");
	ДобавитьПоля(ПоляВыбора, НаборДанных.Ресурсы, ДвижениеДокумента, "Ресурсы");
	ДобавитьПоля(ПоляВыбора, НаборДанных.Реквизиты, ДвижениеДокумента, "Реквизиты");
	
	Если ДополнительнаяНумерация Тогда
		
		НаборДанных.Нумератор = НомераПолей(НаборДанных);
		ДобавитьНомераПолей(ПоляВыбора, НаборДанных.Нумератор);
		
	КонецЕсли;
	
	Если СокрЛП(Прав(ПоляВыбора, 2)) = "," Тогда
		ПоляВыбора = Лев(ПоляВыбора, СтрДлина(ПоляВыбора) - 2);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК КоличествоЗаписейРегистра,
	|	""&ИмяРегистра"" КАК ИмяРегистра,
	|	&Поля
	|ИЗ
	|	&ТекущаяТаблица КАК ТекущаяТаблица
	|ГДЕ
	|	&УсловиеЗапроса
	|{ГДЕ
	|	(&УсловиеКомпоновки)}";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяРегистра", НаборДанных.ПолноеИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Поля", ПоляВыбора);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекущаяТаблица", ДвижениеДокумента.ПолноеИмя());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеЗапроса", СведенияОРегистре.ПолеРегистратора + " = &ДокументВладелец");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеКомпоновки", """" + НаборДанных.ПолноеИмяРегистра + """ В (&СписокРегистров)");
	
	НаборДанных.Вставить("ТекстЗапроса", ТекстЗапроса);
	
	Возврат НаборДанных;
	
КонецФункции

Процедура ДобавитьНомераПолей(ТекстНомеровПолей, Нумераторы)
	
	Для Каждого Нумератор Из Нумераторы Цикл
		ТекстНомеровПолей = ТекстНомеровПолей + ?(ЗначениеЗаполнено(ТекстНомеровПолей), ", ", "") + Нумератор.Значение + " Как "
			+ Нумератор.Ключ;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ГоризонтальныйВариант

Функция ПодготовитьГоризонтальныйВариант(ДокументОтбор, НастройкиКД)
	
	ДвиженияДокумента = РегистрыСДвижениямиДокумента(ДокументОтбор, ДокументОтбор.Метаданные().Движения);
	УстановитьГлобальныеНастройки(ДокументОтбор, ДвиженияДокумента);
	
	Если ДвиженияДокумента.Количество() = 0 Тогда
		Возврат СхемаКомпоновкиДанных;
	КонецЕсли;
	
	ТаблицаРегистров = ТаблицаДвиженийДокумента(ДвиженияДокумента);

	СтруктураПараметровДанных = Новый Структура("СписокРегистров", СформироватьСписокРегистров(ТаблицаРегистров));
	УстановитьПараметрыДанных(НастройкиКД, СтруктураПараметровДанных);

	НаборыДанных = НаборыДанныхДляОтчета(ДокументОтбор, ДвиженияДокумента, ТаблицаРегистров);
	НаборыДанныхСКД = СхемаКомпоновкиДанных.НаборыДанных["Main"].Элементы;
	
	ГлобальныеНастройки = ГлобальныеНастройки();
	
	ИндексНабора = 0;
	Для Каждого НаборДанных Из НаборыДанных Цикл
	
		ИндексНабора   = ИндексНабора + 1;
		ИмяНабора      = "ЗапросПо" + НаборДанных.ПолноеИмяРегистра;
		НаборДанныхСКД = НаборыДанныхСКД.Найти(ИмяНабора);
	
		Если НаборДанныхСКД = Неопределено Тогда
	
			НаборДанныхСКД                = НаборыДанныхСКД.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
			НаборДанныхСКД.Имя            = ИмяНабора;
			НаборДанныхСКД.ИсточникДанных = "ИсточникДанных1";
	
			НаборДанныхСКД.Запрос = НаборДанных.ТекстЗапроса;
	
			ДоопределитьМакетСКД(СхемаКомпоновкиДанных, ИндексНабора, НаборДанных.ВидРегистра,
				НаборДанных.ПредставлениеВидаРегистра, НаборДанных.ИмяРегистра, ГлобальныеНастройки);
	
		КонецЕсли;
	
		ПодготовитьГоризонтальныйВариантНабораДанных(НастройкиКД, НаборДанных, НаборДанныхСКД);
		
	КонецЦикла;
	
	СкрытьПараметрыИОтборы(НастройкиКД);
	Возврат СхемаКомпоновкиДанных;
	
КонецФункции

Процедура ПодготовитьГоризонтальныйВариантНабораДанных(НастройкиКД, ЭлементДанных, НаборДанныхКД)
	
	Если ЭлементДанных.ВидРегистра = "Накопления" Или ЭлементДанных.ВидРегистра = "Сведений" Тогда
		ПодготовитьГоризонтальныйВариантРегистровНакопленияИСведений_Горизонталь(НастройкиКД, НаборДанныхКД, ЭлементДанных);
	ИначеЕсли ЭлементДанных.ВидРегистра = "Бухгалтерии" Тогда
		ПодготовитьГоризонтальныйВариантРегистровБухгалтерии(НастройкиКД, НаборДанныхКД, ЭлементДанных);
	ИначеЕсли ЭлементДанных.ВидРегистра = "Расчета" Тогда
		ПодготовитьГоризонтальныйВариантРегистровРасчета(НастройкиКД, НаборДанныхКД, ЭлементДанных);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьГоризонтальныйВариантРегистровНакопленияИСведений_Горизонталь(НастройкиКД, НаборДанныхКД, ЭлементДанных)
	
	ВидРегистра            = ЭлементДанных.ВидРегистра;
	ПредставлениеВидаРегистра = ЭлементДанных.ПредставлениеВидаРегистра;
	ИмяРегистра            = ЭлементДанных.ИмяРегистра;
	ПредставлениеРегистра  = ЭлементДанных.ПредставлениеРегистра;
	
	СтруктураСтандартныхРеквизитов = ЭлементДанных.СтандартныеРеквизиты;
	СтруктураИзмерений             = ЭлементДанных.Измерения;
	СтруктураРесурсов              = ЭлементДанных.Ресурсы;
	СтруктураРеквизитов            = ЭлементДанных.Реквизиты;
	
	ПользовательскийЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
		НРег(ПредставлениеВидаРегистра), ПредставлениеРегистра);
	
	ГруппировкаДетальныеЗаписи = 
		ДобавитьЭлементСтруктуры(НастройкиКД, "Регистр" + ВидРегистра + "_" + ИмяРегистра, ПользовательскийЗаголовок);
	
	Если СтруктураСтандартныхРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СтандартныеРеквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Стандартные реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураСтандартныхРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураИзмерений.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Измерения");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Измерения'"));
		
		РазместитьГруппуПолейСКД(СтруктураИзмерений, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураРесурсов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Ресурсы");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Ресурсы'"));
		
		РазместитьГруппуПолейСКД(СтруктураРесурсов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Реквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;

КонецПроцедуры

Процедура ПодготовитьГоризонтальныйВариантРегистровРасчета(НастройкиКД, НаборДанныхКД, ЭлементДанных)
	
	ВидРегистра            = "Расчета";
	ПредставлениеВидаРегистра = НСтр("ru = 'Расчета'");
	ИмяРегистра            = ЭлементДанных.ИмяРегистра;
	ПредставлениеРегистра  = ЭлементДанных.ПредставлениеРегистра;
	
	СтруктураСтандартныхРеквизитов = ЭлементДанных.СтандартныеРеквизиты;
	СтруктураИзмерений             = ЭлементДанных.Измерения;
	СтруктураРесурсов              = ЭлементДанных.Ресурсы;
	СтруктураРеквизитов            = ЭлементДанных.Реквизиты;
	
	ПользовательскийЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
		НРег(ПредставлениеВидаРегистра), ПредставлениеРегистра);
	
	ГруппировкаДетальныеЗаписи = ДобавитьЭлементСтруктуры(НастройкиКД,
		"Регистр" + ВидРегистра + "_" + ИмяРегистра, ПользовательскийЗаголовок);
	
	Если СтруктураСтандартныхРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СтандартныеРеквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Горизонтально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураСтандартныхРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураИзмерений.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Измерения");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Измерения'"));
		
		РазместитьГруппуПолейСКД(СтруктураИзмерений, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
		
	КонецЕсли;
	
	Если СтруктураРесурсов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Ресурсы");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Ресурсы'"));
		
		РазместитьГруппуПолейСКД(СтруктураРесурсов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
		
	КонецЕсли;
	
	Если СтруктураРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Реквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВертикальныйВариант

Функция ПодготовитьВертикальныйВариант(ДокументОтбор, НастройкиКД, ДополнительнаяНумерация = Ложь)
	
	ДвиженияДокумента = РегистрыСДвижениямиДокумента(ДокументОтбор, ДокументОтбор.Метаданные().Движения);
	УстановитьГлобальныеНастройки(ДокументОтбор, ДвиженияДокумента);
	
	Если ДвиженияДокумента.Количество() = 0 Тогда
		Возврат СхемаКомпоновкиДанных;
	КонецЕсли;

	ТаблицаРегистров = ТаблицаДвиженийДокумента(ДвиженияДокумента);
	НаборыДанных = НаборыДанныхДляОтчета(ДокументОтбор, ДвиженияДокумента, ТаблицаРегистров, ДополнительнаяНумерация);
	
	СтруктураПараметровДанных = Новый Структура("СписокРегистров", СформироватьСписокРегистров(ТаблицаРегистров));
	УстановитьПараметрыДанных(НастройкиКД, СтруктураПараметровДанных);
	
	НаборыДанныхСКД = СхемаКомпоновкиДанных.НаборыДанных["Main"].Элементы;
	
	ГлобальныеНастройки = ГлобальныеНастройки();
	
	ИндексНабора = 0;
	Для Каждого НаборДанных Из НаборыДанных Цикл
	
		ИндексНабора   = ИндексНабора + 1;
		ИмяНабора      = "ЗапросПо" + НаборДанных.ПолноеИмяРегистра;
		НаборДанныхСКД = НаборыДанныхСКД.Найти(ИмяНабора);
	
		Если НаборДанныхСКД = Неопределено Тогда
	
			НаборДанныхСКД                = НаборыДанныхСКД.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
			НаборДанныхСКД.Имя            = ИмяНабора;
			НаборДанныхСКД.ИсточникДанных = "ИсточникДанных1";
	
			НаборДанныхСКД.Запрос = НаборДанных.ТекстЗапроса;
	
			ДоопределитьМакетСКД(СхемаКомпоновкиДанных, ИндексНабора, НаборДанных.ВидРегистра,
				НаборДанных.ПредставлениеВидаРегистра, НаборДанных.ИмяРегистра, ГлобальныеНастройки);
	
		КонецЕсли;
	
		ПодготовитьВертикальныйВариантНабораДанных(НастройкиКД, НаборДанных, НаборДанныхСКД);
	
	КонецЦикла;
	
	СкрытьПараметрыИОтборы(НастройкиКД);
	Возврат СхемаКомпоновкиДанных;
	
КонецФункции

Процедура ПодготовитьВертикальныйВариантНабораДанных(НастройкиКД, НаборДанных, НаборДанныхКД)
	
	Если НаборДанных.ВидРегистра = "Накопления" Или НаборДанных.ВидРегистра = "Сведений" Тогда
	
		ПодготовитьВертикальныйВариантРегистровНакопленияИСведений(НастройкиКД, НаборДанныхКД, НаборДанных);
	
	ИначеЕсли НаборДанных.ВидРегистра = "Бухгалтерии" Тогда
	
		ПодготовитьГоризонтальныйВариантРегистровБухгалтерии(НастройкиКД, НаборДанныхКД, НаборДанных);
	
	ИначеЕсли НаборДанных.ВидРегистра = "Расчета" Тогда
	
		ПодготовитьВертикальныйВариантРегистровРасчета(НастройкиКД, НаборДанныхКД, НаборДанных);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьВертикальныйВариантРегистровНакопленияИСведений(НастройкиКД, НаборДанныхКД, ЭлементДанных)
	
	ВидРегистра            = ЭлементДанных.ВидРегистра;
	ПредставлениеВидаРегистра = ЭлементДанных.ПредставлениеВидаРегистра;
	ИмяРегистра            = ЭлементДанных.ИмяРегистра;
	ПредставлениеРегистра  = ЭлементДанных.ПредставлениеРегистра;
	
	СтруктураСтандартныхРеквизитов = ЭлементДанных.СтандартныеРеквизиты;
	СтруктураИзмерений             = ЭлементДанных.Измерения;
	СтруктураРесурсов              = ЭлементДанных.Ресурсы;
	СтруктураРеквизитов            = ЭлементДанных.Реквизиты;
	СтруктураНумератора            = ЭлементДанных.Нумератор; 
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
		НРег(ПредставлениеВидаРегистра), ПредставлениеРегистра);
	ГруппировкаДетальныеЗаписи = ДобавитьЭлементСтруктуры(НастройкиКД, "Регистр" + ВидРегистра + "_" + ИмяРегистра, Заголовок);
	
	Если СтруктураНумератора.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Нумератор");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "№ГруппаЭлементаВыводаСКДТекущегоДокумента");
		
		РазместитьГруппуПолейСКД(СтруктураНумератора, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураСтандартныхРеквизитов.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СтандартныеРеквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Стандартные реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураСтандартныхРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураИзмерений.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Измерения");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Измерения'"));
		
		РазместитьГруппуПолейСКД(СтруктураИзмерений, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураРесурсов.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Ресурсы");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Ресурсы'"));
		
		РазместитьГруппуПолейСКД(СтруктураРесурсов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураРеквизитов.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Реквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьВертикальныйВариантРегистровРасчета(НастройкиКД, НаборДанныхКД, ЭлементДанных)
	
	ВидРегистра            = "Расчета";
	ПредставлениеВидаРегистра = НСтр("ru = 'Расчета'");
	ИмяРегистра            = ЭлементДанных.ИмяРегистра;
	ПредставлениеРегистра  = ЭлементДанных.ПредставлениеРегистра;
	
	СтруктураСтандартныхРеквизитов = ЭлементДанных.СтандартныеРеквизиты;
	СтруктураИзмерений             = ЭлементДанных.Измерения;
	СтруктураРесурсов              = ЭлементДанных.Ресурсы;
	СтруктураРеквизитов            = ЭлементДанных.Реквизиты;
	СтруктураНумератора            = ЭлементДанных.Нумератор;
	
	ПользовательскийЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
		НРег(ПредставлениеВидаРегистра), ПредставлениеРегистра);
	
	ГруппировкаДетальныеЗаписи = ДобавитьЭлементСтруктуры(НастройкиКД, "Регистр" + ВидРегистра + "_" + ИмяРегистра,
		ПользовательскийЗаголовок);
	
	Если СтруктураНумератора.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Нумератор");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "№ГруппаЭлементаВыводаСКДТекущегоДокумента");
		
		РазместитьГруппуПолейСКД(СтруктураНумератора, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
		
	КонецЕсли;
	
	Если СтруктураСтандартныхРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СтандартныеРеквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Стандартные реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураСтандартныхРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
		
	КонецЕсли;
	
	Если СтруктураИзмерений.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Измерения");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Измерения'"));
		
		РазместитьГруппуПолейСКД(СтруктураИзмерений, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
		
	КонецЕсли;
	
	Если СтруктураРесурсов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Ресурсы");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Ресурсы'"));
		
		РазместитьГруппуПолейСКД(СтруктураРесурсов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
		
	КонецЕсли;
	
	Если СтруктураРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Реквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Ложь);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", НСтр("ru = 'Реквизиты'"));
		
		РазместитьГруппуПолейСКД(СтруктураРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеСтруктурыОтчета

Процедура ПодготовитьГоризонтальныйВариантРегистровБухгалтерии(НастройкиКД, НаборДанныхКД, ЭлементДанных)
	
	ВидРегистра            = "Бухгалтерии";
	ПредставлениеВидаРегистра = НСтр("ru = 'Бухгалтерии'");
	ИмяРегистра            = ЭлементДанных.ИмяРегистра;
	ПредставлениеРегистра  = ЭлементДанных.ПредставлениеРегистра;
	
	СтруктураСтандартныхРеквизитов = ЭлементДанных.СтандартныеРеквизиты;
	СтруктураИзмерений             = ЭлементДанных.Измерения;
	СтруктураИзмеренийДт           = ЭлементДанных.ИзмеренияДт;
	СтруктураИзмеренийКт           = ЭлементДанных.ИзмеренияКт;
	СтруктураРесурсов              = ЭлементДанных.Ресурсы;
	СтруктураРесурсовДт            = ЭлементДанных.РесурсыДт;
	СтруктураРесурсовКт            = ЭлементДанных.РесурсыКт;
	СтруктураСубконто              = ЭлементДанных.Субконто;
	СтруктураСубконтоДт            = ЭлементДанных.СубконтоДт;
	СтруктураСубконтоКт            = ЭлементДанных.СубконтоКт;
	СтруктураРеквизитов            = ЭлементДанных.Реквизиты;
	
	ПользовательскийЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
		НРег(ПредставлениеВидаРегистра), ПредставлениеРегистра);
	
	ГруппировкаДетальныеЗаписи = ДобавитьЭлементСтруктуры(НастройкиКД,
		"Регистр" + ВидРегистра + "_" + ИмяРегистра, ПользовательскийЗаголовок);
	
	Если СтруктураСтандартныхРеквизитов.Количество() > 0 Тогда
		
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СтандартныеРеквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураСтандартныхРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураИзмерений.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Измерения");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураИзмерений, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураИзмеренийДт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "ИзмеренияДт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураИзмеренийДт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураСубконто.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Субконто");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураСубконто, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураСубконтоДт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СубконтоДт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураСубконтоДт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураРесурсовДт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "РесурсыДт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураРесурсовДт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураИзмеренийКт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "ИзмеренияКт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураИзмеренийКт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураСубконтоКт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "СубконтоКт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураСубконтоКт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураРесурсовКт.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "РесурсыКт");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  РасположениеПоляКомпоновкиДанных.Вертикально);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураРесурсовКт, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураРесурсов.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Ресурсы");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураРесурсов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
	Если СтруктураРеквизитов.Количество() > 0 Тогда
	
		ПараметрыГруппы = Новый Структура;
		ПараметрыГруппы.Вставить("ИмяГруппы",                    "Реквизиты");
		ПараметрыГруппы.Вставить("РасположениеВыбраннойГруппы",  Неопределено);
		ПараметрыГруппы.Вставить("ВыбраннаяГруппаПуста",         Истина);
		ПараметрыГруппы.Вставить("ПредставлениеВыбраннойГруппы", "");
		
		РазместитьГруппуПолейСКД(СтруктураРеквизитов, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра);
	
	КонецЕсли;
	
КонецПроцедуры

Функция ДобавитьПолеНабораДанных(НаборДанных, Поле, Заголовок, ПутьКДанным = Неопределено)
	
	Если ПутьКДанным = Неопределено Тогда
		ПутьКДанным = Поле;
	КонецЕсли;
	
	ПолеНабораДанных             = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	ПолеНабораДанных.Поле        = Поле;
	ПолеНабораДанных.Заголовок   = Заголовок;
	ПолеНабораДанных.ПутьКДанным = ПутьКДанным;
	Возврат ПолеНабораДанных;
	
КонецФункции

Функция ДобавитьВыбранноеПоле(Куда, ИмяИлиПолеКД, Заголовок = "") Экспорт
	
	Если ТипЗнч(Куда) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ВыбранныеПоляКД = Куда.Настройки.Выбор;
	ИначеЕсли ТипЗнч(Куда) = Тип("НастройкиКомпоновкиДанных") Или ТипЗнч(Куда) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		ВыбранныеПоляКД = Куда.Выбор;
	Иначе
		ВыбранныеПоляКД = Куда;
	КонецЕсли;
	
	Если ТипЗнч(ИмяИлиПолеКД) = Тип("Строка") Тогда
		ПолеКД = Новый ПолеКомпоновкиДанных(ИмяИлиПолеКД);
	Иначе
		ПолеКД = ИмяИлиПолеКД;
	КонецЕсли;
	
	ВыбранноеПолеКД      = ВыбранныеПоляКД.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПолеКД.Поле = ПолеКД;
	
	Если Заголовок <> "" Тогда
		ВыбранноеПолеКД.Заголовок = Заголовок;
	КонецЕсли;
	
	Возврат ВыбранноеПолеКД;
	
КонецФункции

Функция ДобавитьВыбранноеПолеГруппа(Куда, ИмяИлиПолеКД, Заголовок = "", Расположение = Неопределено)
	
	Если ТипЗнч(Куда) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ВыбранныеПоляКД = Куда.Настройки.Выбор;
	ИначеЕсли ТипЗнч(Куда) = Тип("НастройкиКомпоновкиДанных") Или ТипЗнч(Куда) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		ВыбранныеПоляКД = Куда.Выбор;
	Иначе
		ВыбранныеПоляКД = Куда;
	КонецЕсли;
	
	Если ТипЗнч(ИмяИлиПолеКД) = Тип("Строка") Тогда
		ПолеКД = Новый ПолеКомпоновкиДанных(ИмяИлиПолеКД);
	Иначе
		ПолеКД = ИмяИлиПолеКД;
	КонецЕсли;
	
	ВыбранноеПолеКД      = ВыбранныеПоляКД.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ВыбранноеПолеКД.Поле = ПолеКД;
	
	Если Расположение <> Неопределено Тогда
		ВыбранноеПолеКД.Расположение = Расположение;
	КонецЕсли;
	
	Если Заголовок <> "" Тогда
		ВыбранноеПолеКД.Заголовок = Заголовок;
	КонецЕсли;
	
	Возврат ВыбранноеПолеКД;
	
КонецФункции

Процедура ДобавитьРесурсРасчетаКоличестваЗаписей(СхемаКД)
	
	ПолеКоличестваЗаписей = СхемаКД.ПоляИтога.Найти("КоличествоЗаписейРегистра");
	Если ПолеКоличестваЗаписей = Неопределено Тогда
		ПолеИтога = СхемаКД.ПоляИтога.Добавить();
		ПолеИтога.Группировки.Добавить("ИмяРегистра");
		ПолеИтога.ПутьКДанным = "КоличествоЗаписейРегистра";
		ПолеИтога.Выражение   = "Сумма(КоличествоЗаписейРегистра)";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура УстановитьНастройки(Группировка, Настройки)

	Для Каждого ЭлементНастройки Из Настройки Цикл
	
		Настройка = Группировка.ПараметрыВывода.Элементы.Найти(ЭлементНастройки.Ключ);
		Если Настройка <> Неопределено Тогда
			УстановитьПараметрВывода(Группировка, ЭлементНастройки.Ключ, ЭлементНастройки.Значение);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Функция ДобавитьЭлементСтруктуры(НастройкиКД, ИмяГруппировки, ПользовательскийЗаголовок)
	
	ГруппировкаПоРегистру = НастройкиКД.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	
	ГруппировкаПоРегистру.Имя                                    = ИмяГруппировки;
	ГруппировкаПоРегистру.ПредставлениеПользовательскойНастройки = ПользовательскийЗаголовок;
	ГруппировкаПоРегистру.Использование                          = Истина;
	
	ГруппировкаПоРегистру.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	ГруппировкаПоРегистру.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	
	СкрытьОтборВТаблицеДвижений(ГруппировкаПоРегистру);
	
	ПолеГруппировкиРегистр = ГруппировкаПоРегистру.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	
	ПолеГруппировкиРегистр.Использование  = Истина;
	ПолеГруппировкиРегистр.Поле           = Новый ПолеКомпоновкиДанных("ИмяРегистра");
	ПолеГруппировкиРегистр.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
	ПолеГруппировкиРегистр.ТипДополнения  = ТипДополненияПериодаКомпоновкиДанных.БезДополнения;
	
	ДетальныеЗаписи = ГруппировкаПоРегистру.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ДетальныеЗаписи.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	ДетальныеЗаписи.Использование = Истина;
	
	Возврат ДетальныеЗаписи;
	
КонецФункции

Процедура РазместитьГруппуПолейСКД(СтруктураПолей, НаборДанныхКД, ГруппировкаДетальныеЗаписи, ПараметрыГруппы, ВидРегистра, ИмяРегистра)
	
	Если ПараметрыГруппы.ВыбраннаяГруппаПуста Тогда
		ЛокальноеИмяГруппы     = Новый ПолеКомпоновкиДанных("");
		ЛокальноеПредставлениеГруппы = "";
	Иначе
		ЛокальноеИмяГруппы           = ПараметрыГруппы.ИмяГруппы;
		ЛокальноеПредставлениеГруппы = ПараметрыГруппы.ПредставлениеВыбраннойГруппы;
	КонецЕсли;
	
	Группа = ДобавитьВыбранноеПолеГруппа(ГруппировкаДетальныеЗаписи, ЛокальноеИмяГруппы, ЛокальноеПредставлениеГруппы,
		ПараметрыГруппы.РасположениеВыбраннойГруппы);
	
	Для Каждого ЭлементСтруктуры Из СтруктураПолей Цикл
	
		Имя           = ЭлементСтруктуры.Ключ;
		Представление = ЭлементСтруктуры.Значение;
		ИмяГруппы     = ПараметрыГруппы.ИмяГруппы;
		
		Если ИмяГруппы <> "СтандартныеРеквизиты" И ИмяГруппы <> "Нумератор" И ИмяГруппы <> "ИзмеренияДт"
			И ИмяГруппы <> "ИзмеренияКт" И ИмяГруппы <> "РесурсыДт" И ИмяГруппы <> "РесурсыКт" И ИмяГруппы <> "СубконтоДт"
			И ИмяГруппы <> "СубконтоКт" И ИмяГруппы <> "Субконто" Тогда
			
			Если ВидРегистра = "Накопления" Тогда
				ОбъектМетаданных = Метаданные.РегистрыНакопления[ИмяРегистра][ИмяГруппы][Имя];
			ИначеЕсли ВидРегистра = "Сведений" Тогда
				ОбъектМетаданных = Метаданные.РегистрыСведений[ИмяРегистра][ИмяГруппы][Имя];
			ИначеЕсли ВидРегистра = "Расчета" Тогда
				ОбъектМетаданных = Метаданные.РегистрыРасчета[ИмяРегистра][ИмяГруппы][Имя];
			ИначеЕсли ВидРегистра = "Бухгалтерии" Тогда
				ОбъектМетаданных = Метаданные.РегистрыБухгалтерии[ИмяРегистра][ИмяГруппы][Имя];
			КонецЕсли;
			
			Если Не ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных) Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
	
		ДобавитьПолеНабораДанных(НаборДанныхКД, Имя, Представление);
		ДобавитьВыбранноеПоле(Группа, Имя, Представление);
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПоля(ТекстПолейВыбора, ИменаПредставленияПолей, ДвижениеДокумента, ИмяКоллекции = "")
	
	Для Каждого Поле Из ИменаПредставленияПолей Цикл
		
		Если Не ЗначениеЗаполнено(ИмяКоллекции) 
			Или ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ДвижениеДокумента[ИмяКоллекции][Поле.Ключ]) Тогда
			
			ТекстПолейВыбора = ТекстПолейВыбора + ?(ЗначениеЗаполнено(ТекстПолейВыбора), ", ", "") + Поле.Ключ;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ИменаПредставленияПолей(ПоляРегистра, Знач ИсключаемыеПоля = Неопределено)
	
	Если ИсключаемыеПоля <> Неопределено Тогда
		ИсключаемыеПоля = СтрРазделить(ИсключаемыеПоля, ", ");
	Иначе
		ИсключаемыеПоля = Новый Массив;
	КонецЕсли;
	
	Результат = Новый Структура;
	Для Каждого ПолеРегистра Из ПоляРегистра Цикл
	
		Если ИсключаемыеПоля.Найти(ПолеРегистра.Имя) = Неопределено Тогда
			Результат.Вставить(ПолеРегистра.Имя, ПолеРегистра.Представление());
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция НомераПолей(НаборДанных)
	
	Результат = Новый Структура;
	
	МаксимальныйНомер = Макс(НаборДанных.СтандартныеРеквизиты.Количество(),
		НаборДанных.Измерения.Количество(),
		НаборДанных.Ресурсы.Количество(),
		НаборДанных.Реквизиты.Количество());
			
	Для Индекс = 1 По МаксимальныйНомер Цикл
		ИндексСтрокой = Формат(Индекс, "ЧГ=0");
		Результат.Вставить("ГруппаЭлементаВыводаСКДТекущегоДокумента" + ИндексСтрокой, ИндексСтрокой);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ВыводСКД

Процедура ОформитьРезультат(ДокументРезультат, ДокументРегистратор)
	
	ГлобальныеНастройки = ГлобальныеНастройки();
	ГлобальныеНастройки.Колонки.Добавить("ТаблицаПримечаний", Новый ОписаниеТипов("ТаблицаЗначений"));
	
	НастройкиКД = КомпоновщикНастроек.ПолучитьНастройки();
	
	Для Каждого Строка Из ГлобальныеНастройки Цикл
		Строка.ТаблицаПримечаний = ТаблицаПримечаний("Регистр" + Строка.ВидРегистра + "_" + Строка.ИмяРегистра, НастройкиКД);
	КонецЦикла;
	
	ТаблицаПримечаний = Новый ТаблицаЗначений;
	Для НомерСтроки = 1 По ДокументРезультат.ВысотаТаблицы Цикл
		
		Для НомерКолонки = 1 По 2 Цикл // поиск заголовка таблицы в первых двух колонках
	
			ИмяОбласти = "R" + Формат(НомерСтроки, "ЧГ=0") + "C" + Формат(НомерКолонки, "ЧГ=0");
			Область    = ДокументРезультат.Область(ИмяОбласти);
			
			НастройкаРегистра = НайтиЗаголовокТаблицы(ГлобальныеНастройки, Область.Текст);
			Если НастройкаРегистра <> Неопределено Тогда
	
				РасшифровкаЗаголовка = Новый Структура;
				РасшифровкаЗаголовка.Вставить("ВидРегистра", НастройкаРегистра.ПредставлениеВидаРегистра);
				РасшифровкаЗаголовка.Вставить("ИмяРегистра", НастройкаРегистра.ИмяРегистра);
				РасшифровкаЗаголовка.Вставить("Регистратор", ДокументРегистратор);
				РасшифровкаЗаголовка.Вставить("ИмяПоляРегистратора", НастройкаРегистра.ИмяПоляРегистратора);
				
				Область.Расшифровка  = РасшифровкаЗаголовка;
				ОтчетыСервер.ВывестиГиперссылку(Область, РасшифровкаЗаголовка, Область.Текст);
				ТаблицаПримечаний = НастройкаРегистра.ТаблицаПримечаний;
				ГраницаПримечаний = 1;
				Прервать;
	
			КонецЕсли;
	
		КонецЦикла;
	
		ИмяОбластиНумератора = "R" + Формат(НомерСтроки, "ЧГ=0") + "C1";
		ОбластьНумератора    = ДокументРезультат.Область(ИмяОбластиНумератора);
	
		Если Не ЭтоЧисло(ОбластьНумератора.Текст) Тогда
	
			Если СтрНайти(ОбластьНумератора.Текст, "№ГруппаЭлементаВыводаСКДТекущегоДокумента") <> 0 Тогда
				ОбластьНумератора.Текст         = "№";
				ОбластьНумератора.ШиринаКолонки = 5;
			КонецЕсли;
			Продолжить;
	
		КонецЕсли;
	
		ОбластьНумератора.Отступ                  = 0;
		ОбластьНумератора.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;

		СтрокаПримечания = ТаблицаПримечаний.Найти(ОбластьНумератора.Текст, "Этаж");
		Если СтрокаПримечания <> Неопределено Тогда

			ЭтажностьСтроки = СтрокаПримечания.Этажность + 1;
			Если ГраницаПримечаний = ЭтажностьСтроки Тогда
				
				ТекстПримечания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В этой строке: %1'"),
					СтрокаПримечания.Примечание + ".");

				ОбластьНумератора.Примечание.Текст = ТекстПримечания;

			Иначе
				ГраницаПримечаний = ГраницаПримечаний + 1;
			КонецЕсли;

		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РазделОформления

Процедура СкрытьПараметрыИОтборы(НастройкиКД)
	
	Настройки = Новый Структура;
	Настройки.Вставить("ВыводитьОтбор",           ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	Настройки.Вставить("ВыводитьПараметрыДанных", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	УстановитьНастройки(НастройкиКД, Настройки);
	
КонецПроцедуры

Процедура СкрытьОтборВТаблицеДвижений(ГруппировкаПоРегистру)
	
	Настройки = Новый Структура;
	Настройки.Вставить("ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	УстановитьНастройки(ГруппировкаПоРегистру, Настройки);
	
КонецПроцедуры

Процедура НастроитьУсловноеОформление(СхемаКД, НастройкиКД)
	
	УсловноеОформление = НастройкиКД.УсловноеОформление.Элементы;
	ОформитьОтрицательныеЧисла(УсловноеОформление);
	
	ДополнительныеСвойства = НастройкиКД.ДополнительныеСвойства;
	Если Не ДополнительныеСвойства.Свойство("ГлобальныеНастройки") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьОстаточныйРегистрНакопления(ДополнительныеСвойства.ГлобальныеНастройки) Тогда
		ОформитьВидДвижения(УсловноеОформление, ВидДвиженияНакопления.Приход);
		ОформитьВидДвижения(УсловноеОформление, ВидДвиженияНакопления.Расход);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОформитьОтрицательныеЧисла(УсловноеОформление)
	
	Элемент = УсловноеОформление.Добавить();
	
	ЭлементОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПараметрыДанных.ОтрицательноеКрасным");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ВыделятьОтрицательные", Истина);
	
	Элемент.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	Элемент.Использование    = Ложь;
	
КонецПроцедуры

Процедура ОформитьВидДвижения(УсловноеОформление, ВидДвижения)
	
	ПолеВидДвижения = Новый ПолеКомпоновкиДанных("ВидДвижения");
	
	Элемент = УсловноеОформление.Добавить();
	
	ЭлементОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПараметрыДанных.ВыделятьВидДвиженияНакопления");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;
	
	ЭлементОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = ПолеВидДвижения;
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = ВидДвижения;
	
	ОформляемыеПоля = Элемент.Поля.Элементы;
	ОформляемоеПоле = ОформляемыеПоля.Добавить();
	ОформляемоеПоле.Поле          = ПолеВидДвижения;
	
	Элемент.ИспользоватьВЗаголовке = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	Элемент.ИспользоватьВЗаголовкеПолей = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста",
		?(ВидДвижения = ВидДвиженияНакопления.Приход, ЦветаСтиля.РезультатУспехЦвет, ЦветаСтиля.ЦветОтрицательногоЧисла));
	Элемент.Использование = Ложь;
	
КонецПроцедуры

Функция ЕстьОстаточныйРегистрНакопления(ГлобальныеНастройки)
	
	Для Каждого ИнформацияПоРегистру Из ГлобальныеНастройки Цикл
		
		Если ИнформацияПоРегистру.ВидРегистра <> "Накопления" Тогда
			Продолжить;
		КонецЕсли;
			
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени("Регистр" + ИнформацияПоРегистру.ВидРегистра + "." + ИнформацияПоРегистру.ИмяРегистра);
		Если ОбъектМетаданных.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ФормированиеПримечаний

Функция ТаблицаПримечаний(ИмяРегистра, НастройкиКД)
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Этаж",       Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Примечание", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Этажность",  Новый ОписаниеТипов("Число"));
	
	МассивВыбранныхПолей = Новый Массив;
	СтруктураГруппировок = НастройкиКД.Структура;
	НайтиГруппировкуРекурсивно(СтруктураГруппировок, МассивВыбранныхПолей, ИмяРегистра);
	
	ПолеНумератора  = Новый ПолеКомпоновкиДанных("Нумератор");
	ЭтажностьСтроки = ЭтажностьСтроки(ПолеНумератора, МассивВыбранныхПолей);
	
	Для Индекс = 0 По ЭтажностьСтроки - 1 Цикл
	
		СтрокаПримечания = "";
	
		Для Каждого ВыбраннаяГруппа Из МассивВыбранныхПолей Цикл
			Если ВыбраннаяГруппа.Поле <> ПолеНумератора Тогда
	
				Заголовок = "";
				Если ВыбраннаяГруппа.Заголовок = НСтр("ru = 'Стандартные реквизиты'") Тогда
					Заголовок = НСтр("ru = 'Стандартный реквизит'");
				ИначеЕсли ВыбраннаяГруппа.Заголовок = НСтр("ru = 'Измерения'") Тогда
					Заголовок = НСтр("ru = 'Измерение'");
				ИначеЕсли ВыбраннаяГруппа.Заголовок = НСтр("ru = 'Ресурсы'") Тогда
					Заголовок = НСтр("ru = 'Ресурс'");
				ИначеЕсли ВыбраннаяГруппа.Заголовок = НСтр("ru = 'Реквизиты'") Тогда
					Заголовок = НСтр("ru = 'Реквизит'");
				ИначеЕсли ВыбраннаяГруппа.Заголовок = НСтр("ru = 'Данные расчета'") Тогда
					Заголовок = НСтр("ru = 'Данные расчета'");
				КонецЕсли;
	
				Элементы = ВыбраннаяГруппа.Элементы;
	
				Если Индекс <= Элементы.Количество() - 1 Тогда
					ВыбранноеПоле = Элементы[Индекс];
					Если ВыбранноеПоле.Использование Тогда
						СтрокаПримечания = СтрокаПримечания + ?(ЗначениеЗаполнено(СтрокаПримечания), ", ", "")
							+ Элементы[Индекс].Заголовок + " (" + Заголовок + ")";
					КонецЕсли;
				КонецЕсли;
	
			КонецЕсли;
		КонецЦикла;
		
		Примечание = Результат.Добавить();
		Примечание.Этаж = Формат(Индекс + 1, "ЧГ=0");
		Примечание.Примечание = СтрокаПримечания;
		Примечание.Этажность = ЭтажностьСтроки;
	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтажностьСтроки(ПолеНумератора, МассивВыбранныхПолей)
	
	ЭтажностьСтроки = 0;
	Для Каждого ВыбраннаяГруппа Из МассивВыбранныхПолей Цикл
	
		Если ВыбраннаяГруппа.Поле = ПолеНумератора Тогда
			Элементы = ВыбраннаяГруппа.Элементы;
			Для Каждого Элемент Из Элементы Цикл
				Если Элемент.Использование Тогда
					ЭтажностьСтроки = ЭтажностьСтроки + 1;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат ЭтажностьСтроки;
	
КонецФункции

#КонецОбласти

#Область Прочее

Процедура НастройкиКДПоУмолчаниюТиповойСхемы(СхемаКомпоновкиДанных, ПараметрыОтчета, НовыеНастройкиКД, КлючВарианта)
	
	СписокРегистровПараметр = СхемаКомпоновкиДанных.Параметры.Найти("СписокРегистров");
	СписокРегистров         = Новый СписокЗначений;
	
	Если КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Свойство("ГлобальныеНастройки") Тогда
		
		ГлобальныеНастройки = КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ГлобальныеНастройки;
		Для Каждого ГлобальнаяНастройка Из ГлобальныеНастройки Цикл
			
			ТекущееЗначениеДляВыбора = "Регистр" + ГлобальнаяНастройка.ВидРегистра + "_" + ГлобальнаяНастройка.ИмяРегистра;
			
			ТекущееЗначениеДляВыбораПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
			НРег(ГлобальнаяНастройка.ПредставлениеВидаРегистра), ГлобальнаяНастройка.ПредставлениеРегистра);
			
			СписокРегистров.Добавить(ТекущееЗначениеДляВыбора, ТекущееЗначениеДляВыбораПредставление, Истина);
			
		КонецЦикла;
		
	КонецЕсли;
	
	СписокРегистровПараметр.УстановитьДоступныеЗначения(СписокРегистров);
	
	Если НовыеНастройкиКД <> Неопределено Тогда
		НовыеНастройкиКД.ДополнительныеСвойства.Вставить("ГлобальныеНастройки",
			КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ГлобальныеНастройки);
	КонецЕсли;
	
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	НастройкиКД = КомпоновщикНастроек.Настройки;
	
	Если КлючВарианта = "Основной" Тогда
		ПодготовитьГоризонтальныйВариант(ПараметрыОтчета.ДокументВладелец, НовыеНастройкиКД);
	ИначеЕсли КлючВарианта = "Дополнительный" Тогда
		ПодготовитьВертикальныйВариант(ПараметрыОтчета.ДокументВладелец, НовыеНастройкиКД, Истина);
	КонецЕсли;
	
	ПараметрОтчета          = НастройкиКД.ПараметрыДанных.Элементы.Найти("ДокументВладелец");
	ПараметрОтчета.Значение = ПараметрыОтчета.ДокументВладелец;
	
	ДобавитьРесурсРасчетаКоличестваЗаписей(СхемаКомпоновкиДанных);
	
КонецПроцедуры

Функция РегистрыСДвижениямиДокумента(Регистратор, Движения)
	
	Результат = Новый Соответствие;
	Для Каждого ОбъектМетаданныхРегистр Из Движения Цикл
		Если Не ПравоДоступа("Просмотр", ОбъектМетаданныхРегистр) Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Вставить(ОбъектМетаданныхРегистр, "Регистратор");
	КонецЦикла;
	
	ДополнительныеРегистры = Новый Соответствие;
	ОтчетОДвиженияхДокументаПереопределяемый.ПриОпределенииРегистровСДвижениями(Регистратор, ДополнительныеРегистры);
	
	Для Каждого ДополнительныйРегистр Из ДополнительныеРегистры Цикл
		Если Не ПравоДоступа("Просмотр", ДополнительныйРегистр.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		Результат.Вставить(ДополнительныйРегистр.Ключ, ДополнительныйРегистр.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьГлобальныеНастройки(Регистратор, ДвиженияДокумента)
	
	ГлобальныеНастройки = Новый Массив;
	КоличествоЗаписей = КоличествоЗаписейПоРегистратору(Регистратор, ДвиженияДокумента);
	
	Для Каждого ИспользуемыйРегистр Из ДвиженияДокумента Цикл
		
		ИспользуемыйРегистр   = ИспользуемыйРегистр.Ключ;
		ИмяРегистра           = ИспользуемыйРегистр.Имя;
		ПредставлениеРегистра = ИспользуемыйРегистр.Представление();
		
		Если ОбщегоНазначения.ЭтоРегистрНакопления(ИспользуемыйРегистр) Тогда
			ВидРегистра            = "Накопления";
			ПредставлениеВидаРегистра = НСтр("ru = 'Накопления'");
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрБухгалтерии(ИспользуемыйРегистр) Тогда
			ВидРегистра            = "Бухгалтерии";
			ПредставлениеВидаРегистра = НСтр("ru = 'Бухгалтерии'");
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрСведений(ИспользуемыйРегистр) Тогда
			ВидРегистра            = "Сведений";
			ПредставлениеВидаРегистра = НСтр("ru = 'Сведений'");
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрРасчета(ИспользуемыйРегистр) Тогда
			ВидРегистра            = "Расчета";
			ПредставлениеВидаРегистра = НСтр("ru = 'Расчета'");
		КонецЕсли;
		
		СтруктураНастроек = Новый Структура;
		СтруктураНастроек.Вставить("ИмяРегистра",            ИмяРегистра);
		СтруктураНастроек.Вставить("ПредставлениеРегистра" , ПредставлениеРегистра);
		СтруктураНастроек.Вставить("ВидРегистра",            ВидРегистра);
		СтруктураНастроек.Вставить("ПредставлениеВидаРегистра", ПредставлениеВидаРегистра);
		СтруктураНастроек.Вставить("КоличествоЗаписей",      КоличествоЗаписей[СтрЗаменить(ИспользуемыйРегистр.ПолноеИмя(), ".", "_")]);
		СтруктураНастроек.Вставить("Использование",          Ложь);
		ГлобальныеНастройки.Добавить(СтруктураНастроек);
		
	КонецЦикла;
	
	КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ГлобальныеНастройки", ГлобальныеНастройки);
	
КонецПроцедуры

Функция ГлобальныеНастройки()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ИмяРегистра",
		Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(128)));
	Результат.Колонки.Добавить("ИмяПоляРегистратора",
		Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(128)));
	Результат.Колонки.Добавить("ПредставлениеРегистра",
		Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(128)));
	Результат.Колонки.Добавить("ВидРегистра",
		Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(32)));
	Результат.Колонки.Добавить("ПредставлениеВидаРегистра",
		Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(32)));
	Результат.Колонки.Добавить("КоличествоЗаписей", Новый ОписаниеТипов("Число"));
	Результат.Колонки.Добавить("ЗаголовокТаблицы",
		Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(256)));
	
	ГлобальныеНастройки = КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ГлобальныеНастройки;
	Для Каждого Настройка Из ГлобальныеНастройки Цикл
	
		НоваяСтрока = Результат.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Настройка);
		
		ЗаголовокТаблицы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 ""%2""'"),
			СокрЛП(НРег(Настройка.ПредставлениеВидаРегистра)), Настройка.ПредставлениеРегистра);
		НоваяСтрока.ЗаголовокТаблицы = ВРег(ЗаголовокТаблицы);
	
	КонецЦикла;
	
	Результат.Индексы.Добавить("ИмяРегистра, ВидРегистра");
	Возврат Результат;
	
КонецФункции

Функция КоличествоЗаписейПоРегистратору(Регистратор, ДвиженияДокумента)
	
	РассчитанноеКоличество = Новый Соответствие;
	Если ДвиженияДокумента.Количество() = 0 Тогда
		Возврат РассчитанноеКоличество;
	КонецЕсли;
	
	ТекстЗапроса = "";
	Для Каждого ИспользуемыйРегистр Из ДвиженияДокумента Цикл
		
		ИспользуемыйРегистрМетаданные = ИспользуемыйРегистр.Ключ;
		ПолноеИмя                     = ИспользуемыйРегистрМетаданные.ПолноеИмя();
		
		ТекстЗапросаШаблон =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	""&ПолноеИмяРегистра"" КАК ПолноеИмяРегистра,
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	&ТекущаяТаблица КАК ТекущаяТаблица
		|ГДЕ
		|	&Условие";
		
		ТекстУсловия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 = &ДокументВладелец", ИспользуемыйРегистр.Значение);
		ТекстЗапросаШаблон = СтрЗаменить(ТекстЗапросаШаблон, "&ПолноеИмяРегистра", СтрЗаменить(ПолноеИмя, ".", "_"));
		ТекстЗапросаШаблон = СтрЗаменить(ТекстЗапросаШаблон, "&Условие", ТекстУсловия);
		ТекстЗапросаШаблон = СтрЗаменить(ТекстЗапросаШаблон, "&ТекущаяТаблица", ПолноеИмя);
		
		Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапросаШаблон;
		Иначе
			ТекстЗапроса = ТекстЗапроса + " ОБЪЕДИНИТЬ ВСЕ " + СтрЗаменить(ТекстЗапросаШаблон, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ");
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументВладелец", Регистратор);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаРезультата Из Результат Цикл
		РассчитанноеКоличество.Вставить(СтрокаРезультата.ПолноеИмяРегистра, СтрокаРезультата.Количество);
	КонецЦикла;
	
	ОтчетОДвиженияхДокументаПереопределяемый.ПриРасчетеКоличестваЗаписей(Регистратор, РассчитанноеКоличество);
	
	Возврат РассчитанноеКоличество;
	
КонецФункции

Функция ТаблицаДвиженийДокумента(ДвиженияДокумента)
	
	ТаблицаРегистров = Новый ТаблицаЗначений;
	Колонки = ТаблицаРегистров.Колонки;
	Колонки.Добавить("Регистр");
	Колонки.Добавить("ИмяРегистра",                   Новый ОписаниеТипов("Строка"));
	Колонки.Добавить("ПредставлениеРегистра",         Новый ОписаниеТипов("Строка"));
	Колонки.Добавить("ВидРегистра",                   Новый ОписаниеТипов("Строка"));
	Колонки.Добавить("ПредставлениеВидаРегистра",     Новый ОписаниеТипов("Строка"));
	Колонки.Добавить("ВидРегистраНакопления",         Новый ОписаниеТипов("Строка"));
	Колонки.Добавить("ПериодичностьРегистраСведений", Новый ОписаниеТипов("Строка"));
	Колонки.Добавить("РежимЗаписиРегистраСведений",   Новый ОписаниеТипов("Строка"));
	Колонки.Добавить("ПериодичностьРегистраРасчета",  Новый ОписаниеТипов("Строка"));
	Колонки.Добавить("ВидРегистраДляУпорядочивания",  Новый ОписаниеТипов("Число", , , 
		Новый КвалификаторыЧисла(1, 0, ДопустимыйЗнак.Неотрицательный)));
	Колонки.Добавить("ПолеРегистратора",              Новый ОписаниеТипов("Строка"));
	
	Для Каждого ДвижениеДокумента Из ДвиженияДокумента Цикл
		
		ПолеРегистратора  = ДвижениеДокумента.Значение;
		ДвижениеДокумента = ДвижениеДокумента.Ключ;
		
		СтрокаТаблицыРегистров = ТаблицаРегистров.Добавить();
		СтрокаТаблицыРегистров.Регистр = ДвижениеДокумента;
		СтрокаТаблицыРегистров.ПолеРегистратора = ПолеРегистратора;
		СтрокаТаблицыРегистров.ИмяРегистра = ДвижениеДокумента.Имя;
		
		Если ОбщегоНазначения.ЭтоРегистрНакопления(ДвижениеДокумента) Тогда
			
			СтрокаТаблицыРегистров.ПредставлениеРегистра        = ДвижениеДокумента.Представление();
			СтрокаТаблицыРегистров.ВидРегистра                  = "Накопления";
			СтрокаТаблицыРегистров.ПредставлениеВидаРегистра       = НСтр("ru = 'Накопления'");
			СтрокаТаблицыРегистров.ВидРегистраНакопления        = Строка(ДвижениеДокумента.ВидРегистра);
			СтрокаТаблицыРегистров.ВидРегистраДляУпорядочивания = 1;
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрБухгалтерии(ДвижениеДокумента) Тогда
			
			СтрокаТаблицыРегистров.ПредставлениеРегистра        = ДвижениеДокумента.Представление();
			СтрокаТаблицыРегистров.ВидРегистра                  = "Бухгалтерии";
			СтрокаТаблицыРегистров.ПредставлениеВидаРегистра       = НСтр("ru = 'Бухгалтерии'");
			СтрокаТаблицыРегистров.ВидРегистраДляУпорядочивания = 3;
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрСведений(ДвижениеДокумента) Тогда
			
			СтрокаТаблицыРегистров.ПредставлениеРегистра         = ДвижениеДокумента.Представление();
			СтрокаТаблицыРегистров.ВидРегистра                   = "Сведений";
			СтрокаТаблицыРегистров.ПредставлениеВидаРегистра        = НСтр("ru = 'Сведений'");
			СтрокаТаблицыРегистров.ПериодичностьРегистраСведений = ДвижениеДокумента.ПериодичностьРегистраСведений;
			СтрокаТаблицыРегистров.РежимЗаписиРегистраСведений   = ДвижениеДокумента.РежимЗаписи;
			СтрокаТаблицыРегистров.ВидРегистраДляУпорядочивания  = 2;
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрРасчета(ДвижениеДокумента) Тогда
			
			СтрокаТаблицыРегистров.ПредставлениеРегистра        = ДвижениеДокумента.Представление();
			СтрокаТаблицыРегистров.ВидРегистра                  = "Расчета";
			СтрокаТаблицыРегистров.ПредставлениеВидаРегистра       = НСтр("ru = 'Расчета'");
			СтрокаТаблицыРегистров.ПериодичностьРегистраРасчета = ДвижениеДокумента.Периодичность;
			СтрокаТаблицыРегистров.ВидРегистраДляУпорядочивания = 4;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаРегистров.Сортировать("ВидРегистраДляУпорядочивания, ПредставлениеРегистра");
	
	Возврат ТаблицаРегистров;
	
КонецФункции

Процедура ДоопределитьМакетСКД(СхемаКД, ИндексНабора, ВидРегистра, ПредставлениеВидаРегистра, ИмяРегистра,
	ГлобальныеНастройки)
	
	СтруктураПоиска = Новый Структура("ВидРегистра, ИмяРегистра", ВидРегистра, ИмяРегистра);
	НастройкиРегистра = ГлобальныеНастройки.НайтиСтроки(СтруктураПоиска);
	Если НастройкиРегистра.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	ПредставлениеРегистра = НастройкиРегистра[0].ПредставлениеРегистра;
	ЗаголовокГруппировки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 ""%2""'"),
		НРег(ПредставлениеВидаРегистра),ПредставлениеРегистра);
	
	Макет       = СхемаКД.Макеты.Добавить();
	Макет.Имя   = "Макет" + Формат(ИндексНабора + 1, "ЧГ=0");
	Макет.Макет = Новый МакетОбластиКомпоновкиДанных;
	
	Параметр           = Макет.Параметры.Добавить(Тип("ПараметрОбластиВыражениеКомпоновкиДанных"));
	Параметр.Имя       = "КоличествоЗаписей";
	Параметр.Выражение = "КоличествоЗаписейРегистра";
	
	МакетГруппировки                = СхемаКД.МакетыГруппировок.Добавить();
	МакетГруппировки.ИмяГруппировки = "Регистр" + ВидРегистра + "_" + ИмяРегистра;
	МакетГруппировки.ТипМакета      = ТипМакетаОбластиКомпоновкиДанных.Заголовок;
	МакетГруппировки.Макет          = "Макет" + Формат(ИндексНабора + 1, "ЧГ=0");
	
	МакетОбласти = Макет.Макет;
	СтрокаМакета = МакетОбласти.Добавить(Тип("СтрокаТаблицыОбластиКомпоновкиДанных"));
	Ячейка       = СтрокаМакета.Ячейки.Добавить();
	
	ОформлениеЯчейки = Ячейка.Оформление.Элементы;
	
	Шрифт = ОформлениеЯчейки.Найти("Шрифт");
	
	Если Шрифт <> Неопределено Тогда
		Шрифт.Значение      = Новый Шрифт("Arial", 14);
		Шрифт.Использование = Истина;
	КонецЕсли;
	
	Размещение = ОформлениеЯчейки.Найти("Размещение");
	
	Если Размещение <> Неопределено Тогда
		Размещение.Значение      = ТипРазмещенияТекстаКомпоновкиДанных.Переносить;
		Размещение.Использование = Истина;
	КонецЕсли;
	
	ОбластьКД          = Ячейка.Элементы.Добавить(Тип("ПолеОбластиКомпоновкиДанных"));
	ОбластьКД.Значение = ЗаголовокГруппировки + " (";
	
	ОбластьКД          = Ячейка.Элементы.Добавить(Тип("ПолеОбластиКомпоновкиДанных"));
	ОбластьКД.Значение = Новый ПараметрКомпоновкиДанных(Параметр.Имя);
	
	ОбластьКД          = Ячейка.Элементы.Добавить(Тип("ПолеОбластиКомпоновкиДанных"));
	ОбластьКД.Значение = ")";
		
КонецПроцедуры

Функция СформироватьСписокРегистров(ТаблицаРегистров)
	
	СписокРегистров = Новый СписокЗначений;
	
	Для Каждого СтрокаТаблицыРегистров Из ТаблицаРегистров Цикл
		
		ЗаголовокГруппировки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Регистр %1 %2'"),
			НРег(СтрокаТаблицыРегистров.ПредставлениеВидаРегистра), СтрокаТаблицыРегистров.ПредставлениеРегистра);
			
		ЭлементОтбора = "Регистр" + СтрокаТаблицыРегистров.ВидРегистра + "_" + СтрокаТаблицыРегистров.ИмяРегистра;
	
		СписокРегистров.Добавить(ЭлементОтбора, ЗаголовокГруппировки);
	
	КонецЦикла;
	
	Возврат СписокРегистров;
	
КонецФункции

Процедура УстановитьПараметрыДанных(НастройкиКД, СтруктураПараметров)
	
	ПараметрыДанных = НастройкиКД.ПараметрыДанных.Элементы;
	
	Для Каждого Параметр Из СтруктураПараметров Цикл 
	
		ТекущийПараметр   = Новый ПараметрКомпоновкиДанных(Параметр.Ключ);
		ТекущийПараметрКД = ПараметрыДанных.Найти(ТекущийПараметр);
	
		Если ТекущийПараметрКД <> Неопределено Тогда
	
			ТекущийПараметрКД.Использование = Истина;
			ТекущийПараметрКД.Значение      = Параметр.Значение;
	
		Иначе
	
			ПараметрДанных               = НастройкиКД.ПараметрыДанных.Элементы.Добавить();
			ПараметрДанных.Использование = Истина;
			ПараметрДанных.Значение      = Параметр.Значение;
			ПараметрДанных.Параметр      = Новый ПараметрКомпоновкиДанных(Параметр.Ключ);
	
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Функция ЭтоЧисло(Знач ПроверяемоеЗначение)
	
	Если ПроверяемоеЗначение = "0" Тогда
		Возврат Истина;
	КонецЕсли;
	ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
	Возврат ОписаниеТипаЧисло.ПривестиЗначение(ПроверяемоеЗначение) <> 0;
	
КонецФункции

Функция ПараметрыРасположенияЭлементовУправления()
	
	Настройки         = Новый Массив;
	ЭлементУправления = Новый Структура;
	
	ЭлементУправления.Вставить("Поле",                     "СписокРегистров");
	ЭлементУправления.Вставить("РастягиватьПоГоризонтали", Ложь);
	ЭлементУправления.Вставить("АвтоМаксимальнаяШирина",   Истина);
	ЭлементУправления.Вставить("Ширина",                   40);
	
	Настройки.Добавить(ЭлементУправления);
	
	Результат = Новый Структура();
	Результат.Вставить("ПараметрыДанных", Настройки);
	
	Возврат Результат;
	
КонецФункции

Функция УстановитьПараметрВывода(КомпоновщикНастроекГруппировка, ИмяПараметра, Значение)
	
	ПараметрСКД = Новый ПараметрКомпоновкиДанных(ИмяПараметра);
	Если ТипЗнч(КомпоновщикНастроекГруппировка) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда	
		ЗначениеПараметра = КомпоновщикНастроекГруппировка.Настройки.ПараметрыВывода.НайтиЗначениеПараметра(ПараметрСКД);
	Иначе
		ЗначениеПараметра = КомпоновщикНастроекГруппировка.ПараметрыВывода.НайтиЗначениеПараметра(ПараметрСКД);
	КонецЕсли;
	
	Если ЗначениеПараметра <> Неопределено Тогда
		ЗначениеПараметра.Использование = Истина;
		ЗначениеПараметра.Значение = Значение;
	КонецЕсли;
	
	Возврат ЗначениеПараметра;
	
КонецФункции

Процедура НайтиГруппировкуРекурсивно(КоллекцияЭлементов, МассивВыбранныхПолей, ЗначениеПоиска)
	
	Для каждого Элемент Из КоллекцияЭлементов Цикл
		Если ТипЗнч(Элемент) = Тип("ГруппировкаКомпоновкиДанных") Тогда
			Если Элемент.Имя = ЗначениеПоиска Тогда
				Если Элемент.Структура.Количество() > 0 Тогда
					ДетальныеЗаписи = Элемент.Структура[0];
					ВыбранныеПоля   = ДетальныеЗаписи.Выбор.Элементы;
					Для Каждого ВыбранноеПоле Из ВыбранныеПоля Цикл
						МассивВыбранныхПолей.Добавить(ВыбранноеПоле);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			НайтиГруппировкуРекурсивно(Элемент.Структура, МассивВыбранныхПолей, ЗначениеПоиска);
		ИначеЕсли ТипЗнч(Элемент) = Тип("ТаблицаКомпоновкиДанных") Тогда
			НайтиГруппировкуРекурсивно(Элемент.Строки, МассивВыбранныхПолей, ЗначениеПоиска);
			НайтиГруппировкуРекурсивно(Элемент.Колонки, МассивВыбранныхПолей, ЗначениеПоиска);
		ИначеЕсли ТипЗнч(Элемент) = Тип("ДиаграммаКомпоновкиДанных") Тогда
			НайтиГруппировкуРекурсивно(Элемент.Серии, МассивВыбранныхПолей, ЗначениеПоиска);
			НайтиГруппировкуРекурсивно(Элемент.Точки, МассивВыбранныхПолей, ЗначениеПоиска);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыОтчета(НастройкиКД, ПользовательскиеНастройкиКД)
	
	Результат = Новый Структура("ДокументВладелец");
	
	ПараметрДокументВладелец = Неопределено;
	
	Если ТипЗнч(ПользовательскиеНастройкиКД) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
		Для Каждого ЭлементКД Из ПользовательскиеНастройкиКД.Элементы Цикл
			Если ТипЗнч(ЭлементКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
				ИмяПараметра = Строка(ЭлементКД.Параметр);
				Если ИмяПараметра = "ДокументВладелец" Тогда
					ПараметрДокументВладелец = ЭлементКД;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ПараметрДокументВладелец = Неопределено Тогда
		ПараметрДокументВладелец = НастройкиКД.ПараметрыДанных.Элементы.Найти("ДокументВладелец");
	КонецЕсли;
	
	Если ПараметрДокументВладелец <> Неопределено Тогда
		Результат.ДокументВладелец = ПараметрДокументВладелец.Значение;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Копирует настройки компоновки данных
//
// Параметры:
//   НастройкиПриемник - НастройкиКомпоновкиДанных, НастройкиВложенногоОбъектаКомпоновкиДанных
//      ГруппировкаКомпоновкиДанных, ГруппировкаТаблицыКомпоновкиДанных, ГруппировкаДиаграммыКомпоновкиДанных,
//      ТаблицаКомпоновкиДанных, ДиаграммаКомпоновкиДанных - коллекция настроек КД, куда копируются настройки
//      НастройкиИсточник	- НастройкиКомпоновкиДанных, НастройкиВложенногоОбъектаКомпоновкиДанных
//      ГруппировкаКомпоновкиДанных, ГруппировкаТаблицыКомпоновкиДанных, ГруппировкаДиаграммыКомпоновкиДанных,
//      ТаблицаКомпоновкиДанных, ДиаграммаКомпоновкиДанных - коллекция настроек КД, откуда копируются настройки.
//
Процедура СкопироватьНастройкиКомпоновкиДанных(НастройкиПриемник, НастройкиИсточник)
	
	Если НастройкиИсточник = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(НастройкиПриемник) = Тип("НастройкиКомпоновкиДанных") Тогда
		Для каждого Параметр Из НастройкиИсточник.ПараметрыДанных.Элементы Цикл
			ЗначениеПараметра = НастройкиПриемник.ПараметрыДанных.НайтиЗначениеПараметра(Параметр.Параметр);
			Если ЗначениеПараметра <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ЗначениеПараметра, Параметр);
			КонецЕсли;
		КонецЦикла;
		Для Каждого Элемент Из НастройкиИсточник.ДополнительныеСвойства Цикл
			НастройкиПриемник.ДополнительныеСвойства.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		ЗаполнитьЗначенияСвойств(НастройкиПриемник, НастройкиИсточник);
		СкопироватьНастройкиКомпоновкиДанных(НастройкиПриемник.Настройки, НастройкиИсточник.Настройки);
		Возврат;
	КонецЕсли;
	
	// Копирование настроек
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиКомпоновкиДанных") Тогда
		
		ЗаполнитьЭлементы(НастройкиПриемник.ПараметрыДанных, НастройкиИсточник.ПараметрыДанных);
		СкопироватьЭлементы(НастройкиПриемник.ПользовательскиеПоля, НастройкиИсточник.ПользовательскиеПоля, Ложь);
		СкопироватьЭлементы(НастройкиПриемник.Отбор,                НастройкиИсточник.Отбор, Ложь);
		СкопироватьЭлементы(НастройкиПриемник.Порядок,              НастройкиИсточник.Порядок, Ложь);
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		СкопироватьЭлементы(НастройкиПриемник.ПоляГруппировки, НастройкиИсточник.ПоляГруппировки, Ложь);
		СкопироватьЭлементы(НастройкиПриемник.Отбор,           НастройкиИсточник.Отбор, Ложь);
		СкопироватьЭлементы(НастройкиПриемник.Порядок,         НастройкиИсточник.Порядок, Ложь);
		ЗаполнитьЗначенияСвойств(НастройкиПриемник, НастройкиИсточник);
		
	КонецЕсли;
	
	СкопироватьЭлементы(НастройкиПриемник.Выбор,              НастройкиИсточник.Выбор, Ложь);
	СкопироватьЭлементы(НастройкиПриемник.УсловноеОформление, НастройкиИсточник.УсловноеОформление, Ложь);
	ЗаполнитьЭлементы(НастройкиПриемник.ПараметрыВывода, НастройкиИсточник.ПараметрыВывода);
	
	// Копирование структуры
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Структура Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Структура.Добавить(ТипЗнч(ЭлементСтруктурыИсточник));
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Структура Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Структура.Добавить(ТипЗнч(ЭлементСтруктурыИсточник));
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ТаблицаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Строки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Строки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Колонки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Колонки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ДиаграммаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Серии Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Серии.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Точки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Точки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет одну коллекцию элементов настроек на основании другой
//
// Параметры:
//   ПриемникЗначения - КоллекцияЗначенийПараметровКомпоновкиДанных, ЗначенияПараметровДанныхКомпоновкиДанных -
//                      коллекция элементов КД, куда копируются параметры
//   ИсточникЗначения - КоллекцияЗначенийПараметровКомпоновкиДанных, ЗначенияПараметровДанныхКомпоновкиДанных -
//                      коллекция элементов КД, откуда копируются параметры
//   ПервыйУровень    - ЗначенияПараметровДанныхКомпоновкиДанных - уровень структуры коллекции элементов КД для
//                                                                 копирования параметров.
//
Процедура ЗаполнитьЭлементы(ПриемникЗначения, ИсточникЗначения, ПервыйУровень = Неопределено)
	
	Если ТипЗнч(ПриемникЗначения) = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		КоллекцияЗначений = ИсточникЗначения;
	Иначе
		КоллекцияЗначений = ИсточникЗначения.Элементы;
	КонецЕсли;
	
	Для каждого ЭлементИсточник Из КоллекцияЗначений Цикл
		Если ПервыйУровень = Неопределено Тогда
			ЭлементПриемник = ПриемникЗначения.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		Иначе
			ЭлементПриемник = ПервыйУровень.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		КонецЕсли;
		Если ЭлементПриемник = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ЭлементИсточник.Использование Тогда
			ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		КонецЕсли;
		Если ТипЗнч(ЭлементИсточник) = Тип("ЗначениеПараметраКомпоновкиДанных") Тогда
			Если ЭлементИсточник.ЗначенияВложенныхПараметров.Количество() <> 0 Тогда
				ЗаполнитьЭлементы(ЭлементПриемник.ЗначенияВложенныхПараметров, ЭлементИсточник.ЗначенияВложенныхПараметров, ПриемникЗначения);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Копирует элементы из одной коллекции в другую
//
// Параметры:
//   ПриемникЗначения - КоллекцияЗначенийПараметровКомпоновкиДанных, ЗначенияПараметровДанныхКомпоновкиДанных -
//                      коллекция элементов КД, куда копируются параметры
//   ИсточникЗначения - КоллекцияЗначенийПараметровКомпоновкиДанных, ЗначенияПараметровДанныхКомпоновкиДанных -
//                      коллекция элементов КД, откуда копируются параметры
//   ОчищатьПриемник  - Булево - признак необходимости очистки приемника.
//
Процедура СкопироватьЭлементы(ПриемникЗначения, ИсточникЗначения, ОчищатьПриемник = Истина)
	
	Если ТипЗнч(ИсточникЗначения) = Тип("УсловноеОформлениеКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ВариантыПользовательскогоПоляВыборКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ОформляемыеПоляКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ЗначенияПараметровДанныхКомпоновкиДанных") Тогда
		СоздаватьПоТипу = Ложь;
	Иначе
		СоздаватьПоТипу = Истина;
	КонецЕсли;
	ПриемникЭлементов = ПриемникЗначения.Элементы;
	ИсточникЭлементов = ИсточникЗначения.Элементы;
	Если ОчищатьПриемник Тогда
		ПриемникЭлементов.Очистить();
	КонецЕсли;
	
	Для каждого ЭлементИсточник Из ИсточникЭлементов Цикл
		
		Если ТипЗнч(ЭлементИсточник) = Тип("ЭлементПорядкаКомпоновкиДанных") Тогда
			// Элементы порядка добавляем в начало
			Индекс = ИсточникЭлементов.Индекс(ЭлементИсточник);
			ЭлементПриемник = ПриемникЭлементов.Вставить(Индекс, ТипЗнч(ЭлементИсточник));
		Иначе
			Если СоздаватьПоТипу Тогда
				ЭлементПриемник = ПриемникЭлементов.Добавить(ТипЗнч(ЭлементИсточник));
			Иначе
				ЭлементПриемник = ПриемникЭлементов.Добавить();
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		// В некоторых коллекциях необходимо заполнить другие коллекции
		Если ТипЗнч(ИсточникЭлементов) = Тип("КоллекцияЭлементовУсловногоОформленияКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Поля, ЭлементИсточник.Поля);
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
			ЗаполнитьЭлементы(ЭлементПриемник.Оформление, ЭлементИсточник.Оформление); 
		ИначеЕсли ТипЗнч(ИсточникЭлементов)	= Тип("КоллекцияВариантовПользовательскогоПоляВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
		КонецЕсли;
		
		// В некоторых элементах коллекции необходимо заполнить другие коллекции
		Если ТипЗнч(ЭлементИсточник) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Варианты, ЭлементИсточник.Варианты);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
			ЭлементПриемник.УстановитьВыражениеДетальныхЗаписей (ЭлементИсточник.ПолучитьВыражениеДетальныхЗаписей());
			ЭлементПриемник.УстановитьВыражениеИтоговыхЗаписей(ЭлементИсточник.ПолучитьВыражениеИтоговыхЗаписей());
			ЭлементПриемник.УстановитьПредставлениеВыраженияДетальныхЗаписей(ЭлементИсточник.ПолучитьПредставлениеВыраженияДетальныхЗаписей ());
			ЭлементПриемник.УстановитьПредставлениеВыраженияИтоговыхЗаписей(ЭлементИсточник.ПолучитьПредставлениеВыраженияИтоговыхЗаписей ());
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиЗаголовокТаблицы(ГлобальныеНастройки, Знач ТекстОбласти)
	
	ТекстОбласти = ВРег(СокрЛП(ТекстОбласти));
	Если Не ЗначениеЗаполнено(ТекстОбласти) Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	ПозицияКоличества = СтрНайти(ТекстОбласти, "(", НаправлениеПоиска.СКонца);
	Если ПозицияКоличества = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	ЗаголовокТаблицы = Лев(ТекстОбласти, ПозицияКоличества - 2);
	
	Возврат ГлобальныеНастройки.Найти(ЗаголовокТаблицы, "ЗаголовокТаблицы");
	
КонецФункции

Процедура ВосстановитьОтборПоГруппировкамРегистров(НастройкиКД)
	
	СтруктураОтчета = НастройкиКД.Структура;
	Для Каждого ЭлементСтруктуры Из СтруктураОтчета Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭлементСтруктуры.Отбор,
			"ИмяРегистра", ЭлементСтруктуры.Имя, ВидСравненияКомпоновкиДанных.Равно,
			НСтр("ru = 'Служебный отбор'"), Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПерерасчетКоличестваДвижений(СписокРегистров, ДокументОтбор, ГлобальныеНастройки)
	
	ДвиженияДокумента = РегистрыСДвижениямиДокумента(ДокументОтбор, ДокументОтбор.Метаданные().Движения);
	КоличествоЗаписей = КоличествоЗаписейПоРегистратору(ДокументОтбор, ДвиженияДокумента);
	
	ОтмеченныеРегистры = Новый СписокЗначений;
	Если ТипЗнч(СписокРегистров) = Тип("Строка") Тогда
		ОтмеченныеРегистры.Добавить(СписокРегистров);
	ИначеЕсли ТипЗнч(СписокРегистров) = Тип("СписокЗначений") Тогда
		ОтмеченныеРегистры = СписокРегистров;
	КонецЕсли;
	
	Для Каждого ГлобальнаяНастройка Из ГлобальныеНастройки Цикл
		
		ПолноеИмя = "Регистр" + ГлобальнаяНастройка.ВидРегистра + "." + ГлобальнаяНастройка.ИмяРегистра;
		ПолноеИмяБезТочки = СтрЗаменить(ПолноеИмя, ".", "_");
		ГлобальнаяНастройка.Вставить("КоличествоЗаписей", КоличествоЗаписей[ПолноеИмяБезТочки]);
		ГлобальнаяНастройка.Вставить("Использование", ОтмеченныеРегистры.НайтиПоЗначению(ПолноеИмяБезТочки) <> Неопределено);
		ГлобальнаяНастройка.Вставить("ИмяПоляРегистратора", ДвиженияДокумента[Метаданные.НайтиПоПолномуИмени(ПолноеИмя)]);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СкрытьНеактуальныеГруппировки(НастройкиКД, ДокументОтбор)
	
	ГлобальныеНастройки = Неопределено;
	Если Не НастройкиКД.ДополнительныеСвойства.Свойство("ГлобальныеНастройки", ГлобальныеНастройки) Тогда
		Возврат;
	КонецЕсли;
	
	СписокРегистровПараметрСКД = НастройкиКД.ПараметрыДанных.Элементы.Найти("СписокРегистров");
	Если СписокРегистровПараметрСКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СписокРегистров = СписокРегистровПараметрСКД.Значение;
	
	ПерерасчетКоличестваДвижений(СписокРегистров, ДокументОтбор, ГлобальныеНастройки);
	
	Для Каждого Элемент Из НастройкиКД.Структура Цикл
		
		Для Каждого НастройкаРегистра Из ГлобальныеНастройки Цикл
			
			Если ВРег(СокрЛП("Регистр" + НастройкаРегистра.ВидРегистра + "_" + НастройкаРегистра.ИмяРегистра)) <> ВРег(СокрЛП(Элемент.Имя)) Тогда
				Продолжить;
			КонецЕсли;
			
			Если НастройкаРегистра.КоличествоЗаписей = 0 Или (СписокРегистровПараметрСКД.Использование И Не НастройкаРегистра.Использование) Тогда
				Элемент.Состояние = СостояниеЭлементаНастройкиКомпоновкиДанных.Отключен;
				Элемент.Структура.Получить(0).Состояние = СостояниеЭлементаНастройкиКомпоновкиДанных.Отключен;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли